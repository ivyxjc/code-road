plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11' apply false
    id "net.saliman.cobertura" version "2.5.4" apply false
    id "org.sonarqube" version "2.6.2" apply false
    id "org.springframework.boot" version '2.1.1.RELEASE' apply false
    id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false
    id 'net.ltgt.apt' version '0.10' apply false
}

ext {
    spring_boot_version = '2.1.1.RELEASE'
    kotlin_version = '1.3.11'
    junit_version = '4.12'
    base_version = '0.0.2-SNAPSHOT'
    commons_lang3_version = '3.7'
    slf4j_version = '1.7.25'
    log4j_slf4j_version = '2.11.0'
    guava_version = '27.0.1-jre'
    lombok_version = '1.16.20'

    hibernate_version = '5.4.0.Final'
}

group 'xyz.ivyxjc'
version base_version

configure(allprojects) { project ->
    group = 'xyz.ivyxjc'
    version = base_version

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'net.ltgt.apt'
    if (name == "pubg4j-web") {
        apply plugin: 'io.spring.dependency-management'
        apply plugin: "net.saliman.cobertura"
        apply plugin: "org.sonarqube"
        apply plugin: "org.springframework.boot"
    }

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

    compileJava {
        sourceCompatibility = 1.8 // can be switched to 11 for testing
        targetCompatibility = 1.8
        options.encoding = "UTF-8"
    }

    compileTestJava {
        sourceCompatibility = 1.8 // can be switched to 11 for testing
        targetCompatibility = 1.8
        options.encoding = "UTF-8"
        options.compilerArgs += "-parameters"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    sourceSets {
    }

    configurations {
        all*.exclude group: 'ch.qos.logback', module: 'logback-core'
        all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
        all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }

    dependencies {
        testImplementation(group: 'junit', name: 'junit', version: junit_version) {
            exclude group: "org.hamcrest", module: "hamcrest-core"
        }
        testImplementation(group: 'org.mockito', name: 'mockito-core', version: '2.23.4') {
            exclude group: "org.hamcrest", module: "hamcrest-core"
        }
        testImplementation(group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.6.0') {
            exclude module: "kotlin-stdlib"
            exclude module: "kotlin-reflect"
            exclude module: "mockito-core"
        }

        testImplementation group: 'junit', name: 'junit', version: junit_version
        //kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
        //apache
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang3_version
        //log
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_slf4j_version
        //guava
        implementation group: 'com.google.guava', name: 'guava', version: guava_version

        //lombok
        implementation group: 'org.projectlombok', name: 'lombok', version: lombok_version
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version

        compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
        testCompileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    }
}
